{-
  TASK 4 - Caesar Cipher
  Implement a Caesar cipher encryption and decryption functions.
  The input is a string (https://docs.ton.org/develop/smart-contracts/guidelines/internal-messages#simple-message-with-comment)
  where the text is encrypted in a cell (after 32bit 00000000 prefix flag), the algorithm rotates the characters and the last ASCII
  character should go to the beginning. Return new text encoded in cell.
-}

int tlen(tuple) asm "TLEN";

() recv_internal() {
}

;; testable
(cell) caesar_cipher_encrypt(int shift, cell text) method_id {
    slice s = text.begin_parse();
    tuple chain = empty_tuple();
    builder b = begin_cell();
    b = b.store_uint(s~load_uint(32), 32);

    int stop? = false;
    do {
        ( int bits, int refs ) = s.slice_bits_refs();
        if (bits) {
            int c = s~load_uint(8);
            if ((c >= 65) & (c <= 90)) {
                c = ((c - 65 + shift) % 26) + 65;
            } elseif ((c >= 97) & (c <= 122)) {
                c = ((c - 97 + shift) % 26) + 97;
            }
            b = b.store_uint(c, 8);
        } elseif (refs) {
            s = s~load_ref().begin_parse();
            chain = cons(b, chain);
            b = begin_cell();
        } else {
            stop? = true;
        }
    } until (stop?);

    cell c = b.end_cell();
    while (chain.tlen()) {
        ( b, chain ) = uncons(chain);
        c = b.store_ref(c).end_cell();
    }
    return c;
}

;; testable
(cell) caesar_cipher_decrypt(int shift, cell text) method_id {
    return caesar_cipher_encrypt(- shift, text);
}
