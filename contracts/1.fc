{-
  TASK 1 - Find branch of the cell tree
  Write the method that will find a branch of the tree by comparing its
  hash with the hash received in the first parameter. When the algorithm finds
  the subtree (branch) whose hash equals the received hash, the root cell of
  this branch should be returned. Return empty cell if the branch is not found.
-}

() recv_internal() {
}

(cell) find_recursive(int hash, cell tree) {
    if (tree.cell_hash() == hash) {
        return tree;
    }
    slice s = tree.begin_parse();
    while (s.slice_refs()) {
        cell found = find_recursive(hash, s~load_ref());
        ifnot (found.null?()) {
            return found;
        }
    }
    return null();
}

;; testable
(cell) find_branch_by_hash(int hash, cell tree) method_id {
    cell found = find_recursive(hash, tree);
    ifnot (found.null?()) {
        return found;
    }
    return begin_cell().end_cell();
}
